0010 ! Menu.gui
0020 ! 
0030 IF %inomads THEN GOSUB INIT_INOMADS
0040 CWDIR ARG(1)
0050 CALL "init.gui"
0060 IF action$="icons" THEN PROCESS "menu.ico","pgmlib/mnu.en" ELSE IF action$<>"prompt" THEN PROCESS "menu.gui","pgmlib/mnu.en"
0070 END 
0080 ! 
0100 ! ^100
0110 INIT_INOMADS:
0120 IF %inomads'url_arg_cnt<>0 THEN LET dataset$=STP(%inomads'url_arg$("ds"),ERR=*NEXT)
0130 OPEN INPUT (HFN,ERR=*BREAK)"ds.conf"
0140 WHILE (1=1)
0150 LET ds$=RCD(LFO,END=*BREAK)
0160 IF ds$="" OR POS(MID(ds$,1,1)="!;#")<>0 THEN CONTINUE
0170 IF dataset$<>"" THEN IF POS(LCS(dataset$)+"="=LCS(ds$))<>1 THEN CONTINUE
0180 ! // if no ds=xxxx passed on the URL, assume first dataset on file
0190 LET path_list$=ARG(ds$,2,"=")
0200 READ DATA FROM path_list$,SEP="," TO %apps_pth$,%data_pth$ ! // apps & data path
0210 BREAK
0220 WEND 
0230 CLOSE (LFO)
0240 IF %apps_pth$="" THEN LET %apps_pth$=LWD; IF POS(MID(%apps_pth$,-1)="/\")=0 THEN LET %apps_pth$+="/"
0250 IF %apps_pth$<>"" THEN DEF ARG(1)=%apps_pth$
0260 IF %data_pth$="" THEN LET %data_pth$=PTH(LWD+"/data",ERR=*NEXT)
0270 IF %data_pth$<>"" AND POS(MID(%data_pth$,-1)="/\")=0 THEN LET %data_pth$+="/"
0280 IF %data_pth$<>"" THEN DEF ARG(2)=%data_pth$
0290 RETURN 
0300 ! ^300
0310 PROMPT:
0320 ICONS:
0330 LET action$=LCS(ARG(PGM(-3),2,";"))
0340 RUN PGN
1000 ! ^1000
1010 PRE_DISPLAY:
1020 PRINT 'SHOW'(-1), ! Hide the base window
1030 CALL "open_iol","mnu/menus.dat",menus,f_main_file$
1040 CALL "open_iol","mnu/mnsystem.dat",mnsystem,f_main_file$
1050 CALL "open_iol","mnu/mnproces.dat",mnproces,f_main_file$
1060 CALL "open_iol","mnu/mnpgm.dat",mnpgm,f_main_file$
1070 EXIT 
1080 ! 
2000 ! ^1000
2010 POST_DISPLAY:
2020 IF %multi_lingual THEN LET menu_var$="LE",_mnu_ctl=17002; GOSUB LANGUAGE_SWITCH ELSE MENU_BAR CLEAR 
2030 GOSUB OPERATOR; IF cmd_str$="END" THEN RETURN 
2040 GOSUB LOAD_LBX1
2050 IF scrn_id$="menu.ico" THEN LET menu_path$="000|000|"; GOSUB LOAD_ICONS
2060 RETURN 
2070 ! 
2100 ON_EXIT:
2110 RELEASE 
3000 ! ^1000 Add (Btn)
3010 ADD:
3020 RETURN 
4000 ! ^1000 Delete (Btn)
4010 DEL:
4020 RETURN 
5000 ! ^1000 Clear and Default (Btn)
5010 CLEAR:
5020 ! 
6000 ! ^1000
6010 LANGUAGE:
6020 MENU_BAR READ menu_var$
6030 LANGUAGE_SWITCH:
6040 SWITCH menu_var$
6050 ! 
6060 LANGUAGE_TO_ENGLISH:
6070 CASE "LE","II"
6080 MESSAGE_LIB "langmsg/messages.en"
6090 LET menu_para$="-["+MSG("&Language")+"]"
6100 LET menu_para$+=",L:["+MSG("&English")+"=C,"+MSG("&Spanish")+"]"
6110 LET %language$="en"
6120 BREAK
6130 ! 
6140 LANGUAGE_TO_SPANISH:
6150 CASE "LS","IE"
6160 MESSAGE_LIB "langmsg/messages.sp"
6170 LET menu_para$="-["+MSG("&Language")+"]"
6180 LET menu_para$+=",I:["+MSG("&English")+","+MSG("&Spanish")+"=C]"
6190 LET %language$="sp"
6200 BREAK
6210 ! 
6220 END SWITCH 
6230 MENU_BAR _mnu_ctl,menu_para$
6240 GOSUB LOAD_LBX1
6250 IF scrn_id$="menu.ico" THEN LET menu_path$="000|000|"; GOSUB LOAD_ICONS
6260 RETURN 
6270 ! 
20000 ! 20000 Input Processing
21000 ! ^1000 Operator Logon
21010 OPERATOR:
21020 ! LET %OPERATOR$="wp"
21030 PROCESS "mnlogon.fmu","mnu.en"
21040 IF STP(%operator$,2)="" THEN LET cmd_str$="END"; RETURN 
21050 RETURN 
21060 ! 
22000 ! ^1000 List Systems and Programs
22010 LOAD_LBX1:
22020 IF scrn_id$="menu.ico" THEN OPEN (HFN)"*memory*"; LET m_menu=LFO ELSE LIST_BOX LOAD lbx1.ctl,""; LET sv_tu=PRM('TU'); SET_PARAM 'TU'
22030 LET main_seq=0 ! 
22040 SELECT *,REC=menus$ FROM menus,KNO=2 BEGIN %mngroup$ END %mngroup$+$FF$ ! read menu entries for this operator
22050 READ (mnsystem,KEY=menus.system$)
22060 IF scrn_id$="menu.ico" THEN LET main_seq$=STR(++main_seq:"000"); WRITE (m_menu,KEY="000|000|"+main_seq$)mnsystem.system$,STP(MSG(mnsystem.name$),2) ELSE LIST_BOX LOAD lbx1.ctl,0,mnsystem.system$+" - "+STP(MSG(mnsystem.name$),2)
22070 LET sub_seq=0 ! 
22080 SELECT *,REC=mnproces$ FROM mnproces BEGIN mnsystem.system$+menus.process$ END mnsystem.system$+menus.process$+$FF$
22090 IF scrn_id$="menu.ico" THEN LET sub_seq$=STR(++sub_seq:"000"); WRITE (m_menu,KEY="000|"+main_seq$+"|"+sub_seq$)mnsystem.system$+mnproces.process$,STP(MSG(mnproces.name$),2) ELSE LIST_BOX LOAD lbx1.ctl,0,mnsystem.system$+" - "+STP(MSG(mnsystem.name$),2)+SEP+mnsystem.system$+mnproces.process$+" - "+STP(MSG(mnproces.name$),2)
22100 LET pgm_seq=0 ! 
22110 SELECT *,REC=mnpgm$ FROM mnpgm BEGIN mnsystem.system$+mnproces.process$ END mnsystem.system$+mnproces.process$+"99"
22120 IF menus.program$<>"" AND menus.program$<>mnpgm.program$ THEN GOTO 22140
22130 IF scrn_id$="menu.ico" THEN LET pgm_seq$=STR(++pgm_seq:"000"); WRITE (m_menu,KEY=main_seq$+"|"+sub_seq$+"|"+pgm_seq$)mnsystem.system$+mnproces.process$+mnpgm.program$,STP(MSG(mnpgm.name$),2) ELSE LIST_BOX LOAD lbx1.ctl,0,mnsystem.system$+" - "+STP(MSG(mnsystem.name$),2)+SEP+mnsystem.system$+mnproces.process$+" - "+STP(MSG(mnproces.name$),2)+SEP+mnsystem.system$+mnproces.process$+mnpgm.program$+" - "+STP(MSG(mnpgm.name$),2)
22140 NEXT RECORD 
22150 NEXT RECORD 
22160 NEXT RECORD 
22170 PRINT 'CURSOR'(0),
22180 IF scrn_id$<>"menu.ico" THEN SET_PARAM 'TU'=sv_tu; LIST_BOX SHOW lbx1.ctl
22190 RETURN 
22200 ! 
22500 LBX1_READ:! ^500
22510 LET id=lbx1.ctl; LIST_BOX READ lbx1.ctl,lbx1$,_eom$,ERR=*RETURN
22520 LET x=POS(SEP=lbx1$,1,2); IF x=0 THEN GOTO *RETURN
22530 READ (mnpgm,KEY=lbx1$(x+1,7),DOM=*RETURN)
22540 LET %patscan=0
22550 GOTO START_NEW_TASK
22560 IF NOT(NUL(mnpgm.library$)) THEN PROCESS mnpgm.pgmid$,mnpgm.library$,ERR=*RETURN ELSE {
22570 NOT_GUI:
22580 LET _obj_w=80,_obj_h=25
22590 PERFORM "*winproc;CENTER_WDW"; LET _x$="",x=0
22600 PRINT 'DIALOGUE'(_obj_c,_obj_l,_obj_w,_obj_h,"",'MODE'($0C0F$)+'FONT'("MS Sans Serif",1,"&"),OPT="S-+"),'CS','SR',
22610 RUN mnpgm.pgmid$,ERR=*RETURN
22620  }
22630 RETURN 
22640 ! 
23000 ! ^1000
23010 START_NEW_TASK:
23020 IF SYS="MS-WINDOWS" THEN GOTO 23050
23030 PROCESS mnpgm.pgmid$,mnpgm.library$
23040 EXIT 
23050 IF %inomads THEN LET i$="/compass/util/menu.gui;RUN_PGM_IN_NEW_TASK"+" -id="+FID(0)+" -FL -arg "+ARG(1)+" "+ARG(2)+" '"+mnpgm.pgmid$+"' '"+mnpgm.library$+"' '"+%operator$+"' '"+%mngroup$+"' '"+%language$+"'"; CALL "*plus/spawn",i$; EXIT 
23060 LET i$=QUO+ARG(0)+QUO+" "+"/compass/util/menu.gui;RUN_PGM_IN_NEW_TASK"+" -id="+FID(0)+" -FL -arg "+ARG(1)+" "+ARG(2)+" '"+mnpgm.pgmid$+"' '"+mnpgm.library$+"' '"+%operator$+"' '"+%mngroup$+"' '"+%language$+"'"
23070 INVOKE HIDE i$
23080 EXIT 
23090 ! 
24000 ! ^1000
24010 RUN_PGM_IN_NEW_TASK:
24012 IF NUL(ARG(4)) THEN GOTO CALLPGM
24020 LET tmp=UNT; OPEN (tmp)ARG(4)
24030 READ (tmp,KEY=PAD(UCS(ARG(3)),12)+"0000",DOM=PGMOFFLINE)
24040 CLOSE (tmp)
24050 LET %operator$=ARG(5),%mngroup$=ARG(6),%language$=ARG(7)
24060 CALL "init.gui"
24070 PRINT 'SHOW'(-1)
24080 PROCESS ARG(3),ARG(4)
24090 RELEASE 
24091 ! 
24092 CALLPGM:
24100 LET %operator$=ARG(5),%mngroup$=ARG(6)
24110 CALL "init.gui"
24111 PRINT 'SHOW'(1)
24112 CALL ARG(3)
24120 RELEASE 
24130 ! 
24140 PGMOFFLINE:
24150 MSGBOX "Program temporarily offline",MSG("Information Message"),"INFO"
24160 RELEASE 
30000 REM 30000
31000 REM ^1000 Load icons menu
31010 LOAD_ICONS:
31020 LIST_BOX LOAD menu.ctl,""
31030 LET menu_def$=""
31040 LET menu_cmd$=""
31050 LET icon_folder$="!32x32/Folders/Folder"
31060 LET icon_back$="!32x32/Arrows/Redo"
31070 LET icon_form$="!32x32/Computer/Application_Form"
31080 LET icon_exit$="!32x32/Buttons/Door_In"
31090 SELECT id$,name$ FROM m_menu BEGIN menu_path$+"001" END menu_path$+"999"
31100 LET menuk$=KEC(m_menu)
31110 IF MID(menuk$,1,3)<>"000" THEN LET icon$=icon_form$ ELSE LET icon$=icon_folder$
31120 LET menu_def$+=icon$+SEP+id$+"-"+name$+ESC
31130 LET menu_cmd$+=MID(menu_path$,1+4*POS("000"=menu_path$))+MID(menuk$,-3)+SEP
31140 NEXT RECORD 
31150 IF MID(menuk$,1,7)="000|000" THEN LET menu_def$+=icon_exit$+SEP+"Exit"+ESC; LET menu_cmd$+="*exit*"+SEP ELSE LET menu_def$=icon_back$+SEP+"Up"+ESC+menu_def$; LET menu_cmd$="*up*"+SEP+menu_cmd$
31160 LIST_BOX LOAD menu.ctl,menu_def$
31170 LET menu.ctl'tblwidth=0
31180 LET menu.ctl'tbl$=menu_cmd$
31190 RETURN 
31200 READ_MENU:
31220 IF menu$="*exit*" THEN LET cmd_str$="END"; RETURN 
31230 IF LEN(menu$)=3+1+3+1+3 THEN FIND (m_menu,KEY=menu$,DOM=*RETURN)id$; FIND (mnpgm,KEY=id$,DOM=*RETURN); PERFORM PGN+";START_NEW_TASK"; RETURN 
31240 IF menu$="*up*" THEN LET menu_path$="000|"+MID(menu_path$,1,3)+"|" ELSE LET menu_path$=menu$+"|"
31250 GOSUB LOAD_ICONS
31260 RETURN 
